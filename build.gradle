// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

/**
 * Finds a reference to the prefered Android SDK directory.
 * Ideally we'd use "android.plugin.sdkFolder" to find this, however this is
 * only possible if we apply the android application plugin. This is not ideal,
 * because it will add a bunch of tasks to do with building an android app,
 * even tnough this build.gradle is not here to build K-App, it is here to
 * apply hacks to play nicely with various upstream projects.
 *
 * Adapted from https://android.googlesource.com/platform/tools/base/+/c8b776289ace4189406b59a9a9c8c9de63271cb0/build-system/gradle/src/main/groovy/com/android/build/gradle/internal/SdkHandler.java
 *   Copyright (C) 2014 The Android Open Source Project
 *   Licensed under the Apache License, Version 2.0
 *   http://www.apache.org/licenses/LICENSE-2.0i
 */
def findSdkPath() {

    Properties properties = new Properties()
    File propFile = file("local.properties")
    if (propFile.exists()) {
        propFile.withReader { properties.load(it) }
    }

    String sdkDirProp = properties.getProperty("sdk.dir");
    if (sdkDirProp != null) {
        return new File(sdkDirProp)
    }

    String envVar = System.getenv("ANDROID_HOME");
    if (envVar != null) {
        return new File(envVar);
    }

    String property = System.getProperty("android.home");
    if (property != null) {
        return new File(property);
    }

    def error = """
Couldn't find Android SDK. Looked for (in order of precedence):
 * sdk.dir in $propFile
 * ANDROID_HOME environment variable
 * android.home Java property
"""

    throw new RuntimeException(error)
}

// Used by the support-v4 libraries build script.
FileCollection getAndroidPrebuilt(String apiLevel) {

    def file = new File("${findSdkPath()}/platforms/android-$apiLevel/android.jar")
    if (!file.exists()) {
        def msg = """
Android SDK for android-$apiLevel not found (required for building support library).
Expected to find it at $file.
Do you have that SDK installed?
(If it is not visible in the Android SDK Manager, "Packages -> Show Obsolete Packages")
"""
        throw new RuntimeException(msg)
    }

    return files(file)

}
